<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   xmlns:gonzo="com.savagelook.Gonzo.*"
			   minWidth="200" minHeight="200" width="800" height="600" 
			   initialize="init();"
			   creationComplete="initUI();"
			   skinClass="com.savagelook.Gonzo.skins.GonzoAppSkin">
	<fx:Script>
		<![CDATA[
			import com.savagelook.Gonzo.GonzoUtils;
			
			import mx.controls.Alert;
			import mx.core.WindowedApplication;
			import mx.events.FlexNativeMenuEvent;
			import mx.managers.CursorManager;

			private static const preHtml:String = "<html><head><style>h1 { color:#ff0000; }</style></head><body>";
			private static const postHtml:String = "</body></html>";
			private static const APP_NAME:String = "Gonzo";
			private static const EDITOR_WIDTH_MINIMUM:Number = 100;
			
			private var cursorID:Number = 0;
			public var currentMarkdownFile:String = "";
			private var currentCssFile:String = "";
			
			[Embed(source="assets/flip_vertical.png")]
			private var resizeCursorSymbol:Class;
			
			protected function init():void
			{
				gonzoMenu.isWin = (Capabilities.os.toLowerCase().indexOf("windows") >= 0);
				gonzoMenu.isMac = (Capabilities.os.toLowerCase().indexOf("mac os") >= 0);	
				gonzoMenu.gonzo = this;
			}

			protected function initUI():void {
				// TODO check for recently loaded files, otherwise
				this.title = APP_NAME + " - Untitled";
			}
			
			private function updateTitle(filename:String):void {
				if (filename != "") {
					this.title = APP_NAME + " - " + filename;
				}
			}
			
			public function updateGonzo(filename:String = ""):void {
				var html:String = Showdown.makeHtml(markdownTextArea.text);
				var rawText:String = StringUtils.stripTags(html);
				var wordCount:uint = StringUtils.wordCount(rawText);
				
				myHtml.htmlText = preHtml + html + postHtml;
				this.statusText.text = "Word Count: " + wordCount.toString();
				this.updateTitle(filename);	
			}
			
			protected function textarea1_keyUpHandler(event:KeyboardEvent):void
			{
				updateGonzo();
			}
			
			private function separatorMouseOver(event:MouseEvent):void {
				trace("separatorMouseOver");
				cursorID = CursorManager.setCursor(resizeCursorSymbol,2,-8,0);
			}
			
			private function separatorMouseOut(event:MouseEvent):void {
				trace("separatorMouseOut");
				CursorManager.removeCursor(cursorID);
			}
			
			private function separatorMouseDown(event:MouseEvent):void {
				trace("separatorMouseDown");

				systemManager.topLevelSystemManager.addEventListener(MouseEvent.MOUSE_MOVE, stageMouseMove, true);
				systemManager.topLevelSystemManager.addEventListener(MouseEvent.MOUSE_UP, function(e:MouseEvent):void {
					trace("stageMouseUp");
					systemManager.topLevelSystemManager.removeEventListener(MouseEvent.MOUSE_MOVE, stageMouseMove, true);
				}, true);
			}
			
			private function stageMouseMove(event:MouseEvent):void {
				if (event.stageX - mainGroupLayout.paddingLeft > EDITOR_WIDTH_MINIMUM &&
					event.stageX < mainGroup.width - mainGroupLayout.paddingRight - EDITOR_WIDTH_MINIMUM) {
					trace("stageMouseMove");
					markdownTextArea.width = event.stageX - 12;
				}
			}

		]]>
	</fx:Script>

	<s:menu>
		<gonzo:GonzoNativeMenu id="gonzoMenu"/>
	</s:menu>	
	
	<s:Group x="0" y="0" width="100%" height="100%" id="topGroup">
		<s:layout>
			<s:VerticalLayout gap="0"/>
		</s:layout>
	
		<s:Group x="0" width="100%" height="100%" id="mainGroup">
			<s:layout>
				<s:HorizontalLayout gap="0" paddingBottom="10" paddingLeft="10" paddingRight="10" paddingTop="10" id="mainGroupLayout"/>
			</s:layout>
			
			<gonzo:GonzoTextArea
				id="markdownTextArea" 
				width="100%" 
				height="100%"
				fontFamily="monaco" 
				fontSize="16" 
				keyUp="textarea1_keyUpHandler(event)"/>
			<s:SkinnableComponent 
				id="mySep" 
				height="100%" 
				width="5" skinClass="com.savagelook.Gonzo.skins.GonzoSeparatorSkin"
								  mouseOver="separatorMouseOver(event);" 
								  mouseOut="separatorMouseOut(event);"
								  mouseDown="separatorMouseDown(event);"
								  />
			<mx:HTML id="myHtml" width="100%" height="100%" 
					 borderColor="#000000"
					 borderStyle="solid" borderVisible="true" tabEnabled="false" tabFocusEnabled="false"/>
		</s:Group>
	</s:Group>
</s:WindowedApplication>
